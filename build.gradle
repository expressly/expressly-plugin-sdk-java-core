apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'groovy'
apply plugin: 'checkstyle'
apply from: "$rootDir/coverage.gradle"

Properties versionProperties = new Properties();
versionProperties.load(new File(rootDir, "src/main/resources/version.properties").newInputStream())
version = versionProperties.getProperty("version")
group = 'com.buyexpressly'

signing {
    required { gradle.taskGraph.hasTask('uploadArchives') }
    sign configurations.archives
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.fasterxml.jackson.core:jackson-databind:2.9.0'
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-joda:2.9.0'
    compile 'joda-time:joda-time:2.9.9'
    compile 'org.apache.httpcomponents:httpclient:4.5.3'

    //testCompile 'junit:junit:4.12'
    testCompile 'org.spockframework:spock-core:1.1-groovy-2.4-rc-3'
    testCompile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'
    testCompile 'org.apache.commons:commons-lang3:3.0'
    testCompile "cglib:cglib:2.2"

    compile 'javax.servlet:servlet-api:2.5'
    checkstyle('com.puppycrawl.tools:checkstyle:8.1')
}

checkstyle {
    configFile = new File(rootDir, "config/checkstyle/checkstyle.xml")
    showViolations = false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

boolean release = true
String snapshotUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
String stagingUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: (release ? stagingUrl : snapshotUrl)) {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'plugin-sdk'
                packaging 'jar'
                description "Expressly Java SDK to integrate e-commerce platforms with the Expressly Network API"
                url 'https://github.com/expressly/expressly-plugin-sdk-java-core'

                scm {
                    url 'scm:git@github.com:expressly/expressly-plugin-sdk-java-core.git'
                    connection 'scm@github.com:expressly/expressly-plugin-sdk-java-core.git'
                    developerConnection 'scm:git@github.com:expressly/expressly-plugin-sdk-java-core.git'
                }

                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'https://github.com/expressly/expressly-plugin-sdk-java-core/master/LICENSE'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'mgsmith57'
                        name 'Marc G. Smith'
                    }
                    developer {
                        id 'gcorre02'
                        name 'Guilherme Ribeiro'
                    }
                }
            }
        }
    }
}
